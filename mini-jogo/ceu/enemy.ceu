class Enemy with
    var _SDL_Rect col_box;
    var int level; 
do    
    var int x0, y0, xf, yf, speed;
    var _SDL_Renderer[] ren;
    ren =_REN;
    var _SDL_Texture[] tex;
    if _rand()%2 then
        tex = _texENEMY1;        
    else
        tex = _texENEMY2;
    end
    var int tex_w,tex_h;
    _SDL_QueryTexture(tex,null,null,&tex_w,&tex_h);

    if _rand()%2 then
        if _rand()%2 then 
            x0 = 0; 
            y0 = _rand()%WIN_H; 
            xf = WIN_W;
            yf = _rand()%WIN_H; 
        else
            x0 = _rand()%WIN_W; 
            y0 = 0; 
            xf = _rand()%WIN_W;
            yf = WIN_H;
        end
    else
        if _rand()%2 then 
            x0 = WIN_W;
            y0 = _rand()%WIN_H; 
            xf = 0; 
            yf = _rand()%WIN_H; 
        else
            x0 = _rand()%WIN_W; 
            y0 = WIN_H;
            xf = _rand()%WIN_W;
            yf = 0; 
        end
    end
    speed = _rand()%25 + 10;
    var int mod = 50-level;//(_sqrt(level));
    if mod < 0 then 
        mod = 0;
    end
    speed = speed + mod;
    var _SDL_Rect tex_r;
    tex_r.x = x0;
    tex_r.y = y0;
    tex_r.w = tex_w;
    tex_r.h = tex_h;

    var _SDL_Rect clip;
    clip.x = 0;
    clip.y = 0;
    clip.w = tex_w/2;
    clip.h = tex_h;
    
    this.col_box.x = tex_r.x;
    this.col_box.y = tex_r.y;
    this.col_box.w = clip.w;
    this.col_box.h = clip.h;
    var bool walk_st; 
    var int d_x = xf - x0;
    var int d_y = yf - y0;
    var int vx = d_x/speed;
    var int vy = d_y/speed;
    par/or do
         every SDL_REDRAW do
            _SDL_RenderCopy(ren, tex, &clip, &this.col_box);
        end
    with
        loop do
            await (speed)ms;
            tex_r.x = tex_r.x + vx;
            tex_r.y = tex_r.y + vy;
            if walk_st then
                clip.x = tex_w/2;
            else
                clip.x = 0;
            end
            walk_st = not walk_st;
            this.col_box.x = tex_r.x;
            this.col_box.y = tex_r.y;
            if tex_r.x > WIN_W or tex_r.x < 0 or tex_r.y > WIN_H or tex_r.y < 0 then
                break;
            end
        end
    end
end


